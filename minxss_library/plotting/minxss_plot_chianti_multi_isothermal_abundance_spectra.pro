;+
; NAME:
;   minxss_plot_chianti_multi_isotemperature_abundance_spectra
;
; PURPOSE:
;   Plot the results of Tom's CHIANTI abundance method in ch_weighted-temp_5.7-7.7_irradiance.sav
;
; INPUTS:
;   None but requires access to ch_weighted-temp_5.7-7.7_irradiance.sav
;
; OPTIONAL INPUTS:
;   None
;
; KEYWORD PARAMETERS:
;   None
;
; OUTPUTS:
;   Plots of coronal and photospheric abundance spectra for 41 temperatures
;
; OPTIONAL OUTPUTS:
;   None
;
; RESTRICTIONS:
;   Requires ch_weighted-temp_5.7-7.7_irradiance.sav file generated by Tom Woods
;
; EXAMPLE:
;   Just run it! 
;
; MODIFICATION HISTORY:
;   2017-03-08: James Paul Mason: Wrote script.
;   2017-03-22: James Paul Mason: Plot updates after review with Tom. 
;-
PRO minxss_plot_chianti_multi_isothermal_abundance_spectra

; Restore the date
restore, getenv('minxss_data') + '/merged/ch_weighted-temp_5.7-7.7_irradiance.sav'

; Make coronal plot
w1 = window(DIMENSIONS = [1200, 800])
p1 = plot(wch_energy, wch_cor[0, *], COLOR = JPMColors(0, totalPointsForGradient = 41), '2', LAYOUT = [1, 2, 1], /CURRENT, $
          TITLE = 'CHIANTI Coronal Abundance Spectra for MinXSS', $
          XTITLE = 'Energy [keV]', $
          NAME = JPMPrintNumber(wch_temp[0]))
FOR i = 1, n_elements(wch_temp) - 1 DO BEGIN
  p1 = plot(wch_energy, wch_cor[i, *], COLOR = JPMColors(i, totalPointsForGradient = 41), '2', /OVERPLOT, $
            NAME = JPMPrintNumber(wch_temp[i]))
ENDFOR
p1.yrange = [1e5, 1e9]
p1.ylog = 1
p1.font_size = 16
tYtitle = text(0.0, 0.5, 'Irradiance [photons s$^{-1}$ cm$^{-2}$ keV$^{-1}$]', orientation = 90, ALIGNMENT = 0.5, VERTICAL_ALIGNMENT = 1, FONT_SIZE = 16)

; Make photospheric plot
p2 = plot(wch_energy, wch_photo[0, *], COLOR = JPMColors(0, totalPointsForGradient = 41), '2', LAYOUT = [1, 2, 2], /CURRENT, $
          TITLE = 'CHIANTI Photosphere Abundance Spectra for MinXSS', $
          XTITLE = 'Energy [keV]', $
          NAME = JPMPrintNumber(wch_temp[0]))
FOR i = 1, n_elements(wch_temp) - 1 DO BEGIN
  p2 = plot(wch_energy, wch_photo[i, *], COLOR = JPMColors(i, totalPointsForGradient = 41), '2', /OVERPLOT, $
            NAME = JPMPrintNumber(wch_temp[i]))
ENDFOR
p2.ylog = 1
p2.yrange = p1.yrange
p2.font_size = 16

; Color bar
colorNames = bytarr(3, n_elements(wch_temp))
FOR i = 0, n_elements(wch_temp) - 1 DO BEGIN
  colorNames[*, i] = JPMColors(i, totalPointsForGradient = 41)
ENDFOR
cbar = colorbar(RGB_TABLE = colorNames, RANGE = minmax(wch_temp), POSITION = [0.93, 0.11, 0.95, 0.9], TEXTPOS = 1, ORIENTATION = 1, $
                TITLE = 'Log Temperature [K]', FONT_SIZE = 12)

p1.save, 'CHIANTI Abundance Spectra.png'
STOP

END